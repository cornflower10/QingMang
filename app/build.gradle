apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.qingmang"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.1.1"

        buildConfigField 'String', 'URL', '"http://47.74.133.184/SERVER_PROJECT"'
        buildConfigField 'String', 'WHITELIST', '"release"'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        develop {
            buildConfigField 'String', 'URL', '"http://47.74.133.184/SERVER_PROJECT/"'
            buildConfigField 'String', 'WHITELIST', '"dev"'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "test", APP_ICON: "@drawable/logo_test", APP_LABEL: "@string/app_name_test"]
        }
        product {}

    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "qingmang_${variant.productFlavors[0].name}-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':uilibrary')
    compile project(':network')
    compile rootProject.ext.dependencies.recyclerView
    compile rootProject.ext.dependencies.design
    compile rootProject.ext.dependencies.appCompat
    compile rootProject.ext.dependencies.support_v4
    compile 'pub.devrel:easypermissions:1.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    //防抖
    compile 'com.github.bumptech.glide:glide:3.8.0'
    //在chrome查看数据库
    compile 'com.facebook.stetho:stetho:1.3.1'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.5.1'

}
